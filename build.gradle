//file:noinspection GroovyAssignabilityCheck

buildscript {
    ext {
        set('springCloudVersion', '2021.0.0')
        set('springBootVersion', '2.6.3')
    }
    dependencies {
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.2"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.cloud.contract' version '3.0.4'
    id "com.github.davidmc24.gradle.plugin.avro" version '1.2.1'
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/"
    }
    maven {
        url "https://jitpack.io"
    }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    integrationTestAnnotationProcessor.extendsFrom annotationProcessor
    integrationTestImplementation.extendsFrom implementation
    integrationRuntimeOnly.extendsFrom runtimeOnly
    integrationCompileOnly.extendsFrom compileOnly

    contractTestAnnotationProcessor.extendsFrom annotationProcessor
    contractTestImplementation.extendsFrom implementation
    contractTestRuntimeOnly.extendsFrom runtimeOnly
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

contracts {
    baseClassForTests = 'com.goals.course.manager.ContractVerifierBaseTestClass'
    testMode = 'WEBTESTCLIENT'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.kafka:spring-kafka:2.8.2'
    implementation 'org.apache.kafka:kafka-clients:3.1.0'
//    implementation 'io.confluent:kafka-avro-serializer'
    implementation 'io.confluent:kafka-avro-serializer:7.0.1'
//    implementation 'org.apache.avro:avro'

    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'io.projectreactor:reactor-test:3.4.18'

// =================================================================================
    integrationCompileOnly 'org.projectlombok:lombok'
    integrationTestAnnotationProcessor 'org.projectlombok:lombok'
    integrationTestImplementation 'org.postgresql:postgresql'
    integrationTestImplementation 'org.testcontainers:testcontainers:1.16.0'
    integrationTestImplementation 'org.testcontainers:postgresql:1.16.0'
    integrationTestImplementation 'org.testcontainers:kafka:1.17.2'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    integrationTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'

    contractTestRuntimeOnly 'org.postgresql:postgresql'
    contractTestImplementation 'org.testcontainers:testcontainers:1.16.0'
    contractTestImplementation 'org.testcontainers:postgresql:1.16.0'
    contractTestImplementation 'org.testcontainers:kafka:1.17.2'
    contractTestImplementation 'io.rest-assured:spring-web-test-client:4.4.0'
    contractTestImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

jar {
    archiveFileName='default-app.jar'
}
bootJar {
    archiveFileName='boot-app.jar'
}

verifierStubsJar {
    archiveFileName="app-${rootProject.name}-contract-stubs.jar"
}
